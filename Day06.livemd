# Day 06

```elixir
# Change working directory to this file's directory.
File.cd(Path.dirname(__ENV__.file))
```

## Parse input

```elixir
input =
  File.read!("inputs/06.txt")
  |> String.trim_trailing()
  |> String.split("\n")

input
```

```elixir
test_input = [
  "Time:      7  15   30",
  "Distance:  9  40  200"
]
```

## Part 1

```elixir
defmodule Parse do
  def parse(input) do
    [times, distances] =
      input
      |> Enum.map(&parse_line/1)

    Enum.zip(times, distances)
  end

  def parse_line(line) do
    line
    |> String.split()
    |> tl()
    |> Enum.map(&String.to_integer/1)
  end
end

test_races = test_input |> Parse.parse()
```

```elixir
defmodule BoatRace do
  def solve(races) do
    races
    |> Enum.map(&winning_hold_times/1)
    |> Enum.map(&Enum.count/1)
    |> Enum.product()
  end

  def winning_hold_times(race_info) do
    Stream.iterate(1, &(&1 + 1))
    |> Stream.drop_while(&(not wins_race?(&1, race_info)))
    |> Stream.take_while(&wins_race?(&1, race_info))
    |> Enum.to_list()
  end

  def wins_race?(hold_time, {time, distance}) do
    hold_time * (time - hold_time) > distance
  end
end

BoatRace.solve(test_races)
```

```elixir
input |> Parse.parse() |> BoatRace.solve()
```

## Part 2 parsing

```elixir
defmodule Parse2 do
  def parse(input) do
    [time, distance] =
      input
      |> Enum.map(&parse_line/1)

    {time, distance}
  end

  def parse_line(line) do
    line
    |> String.replace(" ", "")
    |> String.split(":")
    |> Enum.at(1)
    |> String.to_integer()
  end
end

test_races2 = test_input |> Parse2.parse()
```

## Part 2 naive solve

```elixir
test_races2 |> List.wrap() |> BoatRace.solve()
```

Could do something smarter here but finishes on real input in under 10s.

```elixir
input |> Parse2.parse() |> List.wrap() |> BoatRace.solve()
```

<!-- livebook:{"branch_parent_index":2} -->

## Part 2 done properly

Need points where `hold_time * (time - hold_time) > distance`.

So look at number of points between intersections of `hold_time^2 - time*hold_time + distance = 0`.

Quadratic formula gives us:

```
hold_time = (time Â± sqrt(time*2 - 4 * distance)) / 2
```

```elixir
defmodule BoatRace2 do
  def solve(race_info) do
    {min_hold, max_hold} = get_crossing_points(race_info)
    1 + trunc(Float.floor(max_hold) - Float.ceil(min_hold))
  end

  def get_crossing_points({time, distance}) do
    sqrt_discriminant = (time ** 2 - 4 * distance) ** 0.5

    {
      (time - sqrt_discriminant) / 2,
      (time + sqrt_discriminant) / 2
    }
  end
end

test_races2 |> BoatRace2.solve()
```

```elixir
input |> Parse2.parse() |> BoatRace2.solve()
```
